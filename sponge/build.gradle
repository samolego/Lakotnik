import org.spongepowered.gradle.vanilla.repository.MinecraftPlatform

plugins {
    id("org.spongepowered.gradle.vanilla") version "0.2.1-SNAPSHOT"
}

repositories {
}

dependencies {
    compileOnly("org.spongepowered:mixin:0.8.3-SNAPSHOT")
    compileOnly("org.spongepowered:spongeapi:8.0.0-SNAPSHOT")

    // It doesn't want to compile without this
    testCompileOnly("org.spongepowered:mixin:0.8.3-SNAPSHOT")

    compileOnly project(":common")
}

minecraft {
    version(rootProject.minecraft_version)
    platform(MinecraftPlatform.JOINED)

    runs {
        server()
        client()
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : rootProject.mod_name,
                "Specification-Vendor"    : "",
                "Specification-Version"   : rootProject.mod_version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "",
                "MixinConfigs"            : "${rootProject.mod_name}.mixins.json",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    from project(":common").sourceSets.main.resources
    inputs.property "version", rootProject.version
    inputs.property "mod_name", rootProject.mod_name

    filesMatching("plugins.json") {
        expand(
            "version": rootProject.version,
            "mod_name": rootProject.mod_name
        )
    }
}

tasks.withType(JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}

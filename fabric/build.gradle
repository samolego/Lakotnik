plugins {
    id "fabric-loom" version "0.10-SNAPSHOT"
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Permission API
    modImplementation(include('me.lucko:fabric-permissions-api:0.1-SNAPSHOT'))
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("../run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("../run")
        }
    }
}

processResources {
    inputs.property "version", rootProject.version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }

    inputs.property "refmap", "lakotnik-${project.name}-${rootProject.minecraft_version}-refmap.json"

    filesMatching("lakotnik.mixins.json") {
        expand "refmap": "lakotnik-${project.name}-${rootProject.minecraft_version}-refmap.json"
    }
}
